//mosok battle rifle. bap bap bapbapbap.

actor Mosok41 : Shotgun replaces Shotgun 2001
{
	weapon.ammotype "BulletAmmo"
	weapon.ammogive 15
	inventory.pickupmessage "You got the Mosok 41 battle rifle!"
	decal "BulletChip"
	attacksound "weapons/mosok"
	tag "Mosok 41"
	+weapon.noautofire
	states
	{
	Ready:
		M41G A 0 ACS_ExecuteAlways(69, 0, 912)
		M41G A 0 A_GiveInventory("MosokAccuracy", 1)
		M41G A 1 A_WeaponReady
		goto Ready+1
	Select:
		M41G A 1 A_Raise
		wait
	Deselect:
		M41G A 1 A_Lower
		wait
	Fire:
		M41G A 0 offset(0, 32) A_JumpIfInventory("MosokAccuracy", 1, "Fire2")
		M41G A 0 A_FireBullets(3.75, 3.5, -1, 9, "BulletPuff")
		M41G B 2 bright offset(0, 32) A_GunFlash
		M41G A 1
		goto Recoil
	Fire2:
		M41G A 0 A_FireBullets(0.25, 0.25, -1, 9, "BulletPuff")
		M41G B 2 bright offset(0, 32) A_GunFlash
		M41G A 1 A_TakeInventory("MosokAccuracy", random(0,1))
		goto Recoil
	Recoil:
		M41G A 1 offset(0, 36)
		M41G A 1 offset(0, 38) 
		M41G A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 42) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 44) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 46) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 48) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 46) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 44) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 42) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 38) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 36) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB)
		M41G A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
		goto Ready+1
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		goto LightDone
	Spawn:
		M41L A -1
		stop
	}
}

actor MosokAccuracy : Inventory
{
	inventory.amount 1
	inventory.maxamount 1
	+inventory.ignoreskill
	+undroppable
}