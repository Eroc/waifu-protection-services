//ZR68 compact assault rifle. buddabuddabudda.

actor ZR68c : Chaingun replaces Chaingun
{
	weapon.ammotype "ZR68CMag"
	weapon.ammotype2 "BulletAmmo"
	weapon.ammogive 0
	weapon.ammogive2 30
	inventory.pickupmessage "You got the ZR68 compact assault rifle!"
	attacksound ""
	tag "ZR68c"
	weapon.ammouse 1
	weapon.slotnumber 4
	decal "BulletChip"
	+weapon.ammo_optional
	states
	{
	Ready:
		ZR68 A 0 A_JumpIfInventory("ReloadItem", 1, "Reload")
		ZR68 A 1 A_WeaponReady
		goto Ready
	Select:
		ZR68 A 1 A_Raise
		loop
	Deselect:
		ZR68 A 1 A_Lower
		loop
	Fire:
		ZR68 C 0 A_JumpIfNoAmmo("DryFire")
		ZR68 C 0 A_PlaySound("zr68/cfire", 1)
		ZR68 C 0 A_RailAttack(5*random(1,3), 0, 1, none, none, RGF_SILENT, 0, "ZRBulletPuff", 0.0, 0.0)
		ZR68 C 0 A_Quake(1, 3, 0, 32)
		ZR68 C 1 bright A_GunFlash
		ZR68 D 1 bright
		ZR68 A 1
		ZR68 A 1 A_ReFire
		goto Ready
	Hold:
		ZR68 C 0 A_JumpIfNoAmmo("DryFire")
		ZR68 C 0 A_PlaySound("zr68/cfire", 1)
		ZR68 C 0 A_RailAttack(5*random(1,3), 0, 1, none, none, RGF_SILENT, 0, "ZRBulletPuff", 2.5, 2.0)
		ZR68 C 0 A_Quake(1, 3, 0, 32)
		ZR68 C 1 bright A_GunFlash
		ZR68 D 1 bright
		ZR68 A 1
		ZR68 A 1 A_ReFire
		goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		goto LightDone
	DryFire:
		ZR68 B 1 offset(0, 33) A_PlaySound("zr68/dryfire", 1)
		ZR68 B 1 offset(0, 34)
		ZR68 A 4 offset(0, 32)
		goto Ready
	Spawn:
		ZR68 Z -1
		stop
	Reload:
		ZR68 A 1 A_WeaponReady
		ZR68 A 0 A_JumpIfInventory("ZR68CMag", 0, "Ready")
		ZR68 A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		ZR68 A 0 A_JumpIfInventory("ZR68CMag", 1, "Reload2")
		ZR68 A 0 A_PlaySound("zr68/reload", 1)
		ZR68 P 2
		ZR68 Q 2
		ZR68 R 2
		ZR68 S 2
		ZR68 T 16
		ZR68 U 2
		ZR68 V 2
		ZR68 W 2
		ZR68 M 3
		ZR68 N 10
		ZR68 O 3
		ZR68 A 6
		ZR68 B 1 offset(0, 34)
		ZR68 B 1 offset(0, 36)
		ZR68 B 1 offset(0, 38)
		ZR68 B 2 offset(0, 40)
		ZR68 A 2 offset(0, 38)
		ZR68 A 1 offset(0, 36)
		ZR68 A 1 offset(0, 34)
		ZR68 A 1 offset(0, 32)
	Loading:
		ZR68 A 0 A_TakeInventory("BulletAmmo", 1)
		ZR68 A 0 A_GiveInventory("ZR68CMag", 1)
		ZR68 A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		ZR68 A 0 A_JumpIfInventory("ZR68CMag", 30, "Ready")
		loop
	Reload2:
		ZR68 A 0 A_PlaySound("zr68/reload2", 1)
		ZR68 E 2
		ZR68 F 2
		ZR68 G 2
		ZR68 H 2
		ZR68 I 16
		ZR68 J 2
		ZR68 K 2
		ZR68 L 2
		ZR68 M 3
		ZR68 N 10
		ZR68 O 3
	Loading2:
		ZR68 A 0 A_TakeInventory("BulletAmmo", 1)
		ZR68 A 0 A_GiveInventory("ZR68CMag", 1)
		ZR68 A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		ZR68 A 0 A_JumpIfInventory("ZR68CMag", 0, "Ready")
		loop
	}
}

actor ZRBulletPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFONACTORS
	states
	{
	Crash:
		PUFF A 4 bright
		PUFF BCD 4
		stop
	Spawn:
	Melee:
	XDeath:
		TNT1 A 0
		stop
	}	
}

actor ZR68CMag : Ammo
{
	inventory.amount 1
	inventory.maxamount 31
	ammo.backpackamount 0
	ammo.backpackmaxamount 31
	inventory.icon "CLIPA0"
}