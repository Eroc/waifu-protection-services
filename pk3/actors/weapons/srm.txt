//SRM submachinegun. brrrrrrp.

actor SRM : Chaingun
{
	weapon.ammotype "SRMMag"
	weapon.ammotype2 "BulletAmmo"
	weapon.ammogive2 0
	attacksound "srm/fire"
	weapon.ammogive 50
	decal "BulletChip"
	weapon.slotnumber 2
	tag "SRM"
	+weapon.ammo_optional
	states
	{
	Spawn:
		SRMP A -1
		stop
	Ready:
		SRMG A 0 A_JumpIfInventory("ReloadItem", 1, "Reload")
		SRMG A 1 A_WeaponReady
		loop
	Select:
		SRMG A 0 A_Raise
		SRMG A 1 A_Raise
		loop
	Deselect:
		SRMG A 0 A_Lower
		SRMG A 1 A_Lower
		loop
	Fire:
		SRMG A 0 A_JumpIfNoAmmo("DryFire")
		SRMG A 0 A_Jump(128, "Fire2")
		SRMG A 0 A_FireBullets(5.0, 4.5, -1, 4, "BulletPuff")
		SRMG A 0 A_Quake(1, 2, 0, 32)
		SRMF A 1 bright A_GunFlash
		SRMF B 1 bright
		SRMG B 1 A_ReFire
		goto Ready
	Fire2:
		SRMG A 0 A_FireBullets(5.0, 4.5, -1, 4, "BulletPuff")
		SRMG A 0 A_Quake(1, 2, 0, 32)
		SRMF C 1 bright A_GunFlash
		SRMF D 1 bright
		SRMG B 1 A_ReFire
		goto Ready
	Flash:
		TNT1 A 1 A_Light1
		goto lightdone
	DryFire:
		SRMG A 2 A_PlaySound("srm/dryfire", 1)
		SRMG B 6
		goto Ready
	Reload:
		SRMG A 1 A_WeaponReady
		SRMG A 0 A_JumpIfInventory("SRMMag", 0, "Ready")
		SRMG A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		SRMG A 0 A_JumpIfInventory("SRMMag", 1, "Reload2")
		SRMG A 1 offset(0, 40)
		SRMG A 1 offset(0, 56)
		SRMG A 1 offset(0, 88)
		SRMG A 0 A_PlaySound("srm/reload", 1)
		TNT1 AAA 28
		SRMG A 1 offset(0, 88)
		SRMG A 1 offset(0, 56)
		SRMG A 1 offset(0, 40)
		SRMG A 1 offset(0, 32)
	Loading:
		TNT1 A 0 A_TakeInventory("BulletAmmo", 1)
		TNT1 A 0 A_GiveInventory("SRMMag", 1)
		TNT1 A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		TNT1 A 0 A_JumpIfInventory("SRMMag", 50, "Ready")
		loop
	Reload2:
		SRMG A 1 offset(0, 40)
		SRMG A 1 offset(0, 56)
		SRMG A 1 offset(0, 88)
		SRMG A 0 A_PlaySound("srm/reload", 1)
		TNT1 AAA 28
		SRMG A 1 offset(0, 88)
		SRMG A 1 offset(0, 56)
		SRMG A 1 offset(0, 40)
		SRMG A 1 offset(0, 32)
	Loading2:
		TNT1 A 0 A_TakeInventory("BulletAmmo", 1)
		TNT1 A 0 A_GiveInventory("SRMMag", 1)
		TNT1 A 0 A_JumpIfInventory("BulletAmmo", 1, 1)
		goto Ready
		TNT1 A 0 A_JumpIfInventory("SRMMag", 0, "Ready")
		loop
	}
}

actor SRMMag : Ammo
{
	inventory.amount 1
	inventory.maxamount 51
	ammo.backpackamount 0
	ammo.backpackmaxamount 51
	inventory.icon "CLIPA0"
}